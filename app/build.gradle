plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
}

def gitCommitHash = "git rev-parse --verify --short HEAD".execute().text.trim()
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    println("Loading keystore properties from ${keystorePropertiesFile.absolutePath}")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'org.dianqk.ruslin'
    compileSdk project.compile_sdk_version
    ndkVersion project.ndk_version
    buildToolsVersion project.build_tools_version

    defaultConfig {
        applicationId "org.dianqk.ruslin"
        minSdk project.min_sdk_version
        targetSdk project.target_sdk_version
        versionCode 3
        versionName "0.0.1-pre-alpha.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    signingConfigs {
        release {
            if (keystorePropertiesFile.exists()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
            enableV3Signing = true
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            ndk {
                debugSymbolLevel 'FULL'
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def abi = output.getFilter(output.ABI) ?: "universal"
            output.outputFileName = "Ruslin-${defaultConfig.versionName}-${abi}-${gitCommitHash}-${variant.buildType.name}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                "-opt-in=kotlin.RequiresOptIn",
                "-Xjvm-default=all"
        ]
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        // https://developer.android.com/jetpack/androidx/releases/compose-kotlin
        kotlinCompilerExtensionVersion '1.4.0'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    bundle {
        storeArchive {
            enable = false
        }
    }
}

dependencies {

    implementation project(':mdrender')
    implementation project(':uniffi')

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_runtime_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_runtime_version"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.work:work-runtime-ktx:2.7.1'

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"

    implementation 'androidx.navigation:navigation-compose:2.5.3'
    implementation "androidx.compose.material:material-icons-core:$material_version"
    implementation "androidx.compose.material:material-icons-extended:$material_version"
    implementation "androidx.compose.material:material:$material_version"
    implementation "androidx.compose.material3:material3:$material3_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.accompanist:accompanist-webview:$accompanist_version"

    implementation 'com.google.android.material:material:1.8.0'

    implementation 'com.google.dagger:hilt-android:2.44.2'
    implementation 'androidx.webkit:webkit:1.6.0'
    kapt 'com.google.dagger:hilt-compiler:2.44.2'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    // https://github.com/google/dagger/issues/2601#issuecomment-1174506373
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    implementation 'androidx.hilt:hilt-work:1.0.0'

    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}
